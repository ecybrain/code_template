apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'checkstyle'

sourceCompatibility=1.8
version='1.0'

configurations {
    checkstyleConfig
}

def versions = [
    checkstyle: '6.2',
    junit: '4.12',
    log4j: '1.2.17',
]

dependencies {
    checkstyleConfig('com.puppycrawl.tools:checkstyle:6.2') {
        transitive = false
    }
}

checkstyle {
    showViolations = true
    sourceSets = [sourceSets.main]
    toolVersion = "${versions.checkstyle}"
    config = resources.text.fromArchiveEntry(configurations.checkstyleConfig, 'google_checks.xml')
}
                  
ext {
    mainClass='HelloWorld'
}

jar {
    manifest {
        attributes 'Main-Class': mainClass,
                   'Implementation-Title': 'Hello world',
                   'Implementation-Version': version,
                   'Implementation-Vendor': "ecybrain.com",
                   'Build-Date': new Date().getDateTimeString(),
                   'Build-With': "gradle-${project.getGradle().getGradleVersion()}, groovy-${GroovySystem.getVersion()}, log4j-${versions.log4j}, junit-${versions.junit}",
                   'Created-By':'Java ' + System.getProperty('java.version') +' (' + System.getProperty('java.vendor') +')'
    }
}

repositories {
    mavenCentral()

    maven {
        url "https://repo1.maven.org/maven2/"
    }
}

dependencies {

    compile (
        "log4j:log4j:${versions.log4j}"
    )

    testCompile (
        "junit:junit:${versions.junit}"
    )

}

task execute(type:JavaExec) {
    main = mainClass
    classpath = sourceSets.main.runtimeClasspath
}
